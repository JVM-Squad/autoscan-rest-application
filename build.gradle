buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id "com.diffplug.eclipse.apt" version "3.42.2"
    id "io.micronaut.library" version '4.0.4'
    id "io.micronaut.aot" version "4.0.4"
    id "org.sonarqube" version "4.3.1.3277"
    id "io.freefair.lombok" version "8.3"
    id 'jacoco'
}

ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

allprojects {
    apply plugin: 'java'
    apply plugin: 'signing'
    apply plugin: 'maven-publish'
    apply plugin: 'io.freefair.lombok'

    repositories {
        mavenCentral()

        maven {
            url 'https://oss.sonatype.org/service/local/repositories/releases/content/'
            mavenContent{
                releasesOnly()
            }
        }
        maven {
            url 'https://oss.sonatype.org/content/repositories/snapshots/'
            mavenContent{
                snapshotsOnly()
            }
        }
    }

    sonarqube {
        properties {
            property "sonar.projectKey", "FinTrack:API"
            property "sonar.organization", "jongsoft"
            property "sonar.host.url", "https://sonarcloud.io"
            property "sonar.jacoco.reportPaths", "${buildDir}/reports/jacoco/report.xml"

            // Hack to preven sonar reporting issues in test code
            property "sonar.issue.ignore.multicriteria", "e1"
            property "sonar.issue.ignore.multicriteria.e1.resourceKey", "src/test/java/**/*"
            property "sonar.issue.ignore.multicriteria.e1.ruleKey", "*"
        }
    }

    dependencies {
        annotationProcessor "io.micronaut.data:micronaut-data-hibernate-jpa:$micronautVersion"
        annotationProcessor "org.projectlombok:lombok"
        annotationProcessor "io.micronaut:micronaut-inject-java:$micronautVersion"
        testAnnotationProcessor "io.micronaut:micronaut-inject-java:$micronautVersion"
    }
}

subprojects {
    apply plugin: 'jacoco'

    /**
     * Compiler configuration
     */

    java {
        compileJava.options.encoding = 'UTF-8'
        sourceCompatibility = JavaVersion.toVersion("20")
        targetCompatibility = JavaVersion.toVersion("20")

        withJavadocJar()
        withSourcesJar()
    }

    micronaut {
        runtime "jetty"
        testRuntime "junit5"
        processing {
            incremental(true)
            annotations("com.jongsoft.finance.*")
        }
    }

    /**
     * Test configuration Section
     */
    test {
        useJUnitPlatform()

        finalizedBy jacocoTestReport
    }

    jacocoTestReport {
        dependsOn test

        reports {
            xml.destination file("${buildDir}/reports/jacoco/report.xml")
        }
    }

    jacocoTestCoverageVerification {
        violationRules {
            rule {
                limit {
                    minimum = 0.6
                }
            }
        }
    }

    javadoc {
        dependsOn delombok

        enabled = isReleaseVersion

        options.addBooleanOption('html5', true)
        options.addStringOption('-release', '20')
    }

    publishing {
        repositories {
            maven {
                def releaseRepo = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
                def snapshotRepo = "https://oss.sonatype.org/content/repositories/snapshots/"
                url = isReleaseVersion ? releaseRepo : snapshotRepo
                credentials {
                    username = System.getenv('OSSRH_USERNAME')
                    password = System.getenv('OSSRH_PASSWORD')
                }
            }
        }

        publications {
            mavenJava(MavenPublication) {
                from components.java
                pom {
                    description = 'The REST-API for FinTrack'
                    url = 'https://fintrack.jongsoft.com/'
                    name = project.name
                    scm {
                        connection = 'scm:git:git@bitbucket.org:jongsoftdev/fintrack-application.git'
                        developerConnection = 'scm:git:git@bitbucket.org:jongsoftdev/fintrack-application.git'
                        url = 'https://bitbucket.org/jongsoftdev/fintrack-application/src/master/'
                    }
                    licenses {
                        license {
                            name = 'MIT License'
                            url = 'http://www.opensource.org/licenses/mit-license.php'
                        }
                    }
                    developers {
                        developer {
                            id = 'gjong'
                            name = 'Gerben Jongerius'
                            email = 'g.jongerius@jong-soft.com'
                        }
                    }
                }
            }
        }
    }

    signing {
        sign publishing.publications.mavenJava
    }

    dependencies {
        implementation("io.micronaut:micronaut-inject")
        implementation("io.micronaut.validation:micronaut-validation")
        implementation("io.micronaut:micronaut-runtime")
        implementation("javax.annotation:javax.annotation-api")
        implementation("io.micronaut:micronaut-http-client")

        implementation("io.micronaut.serde:micronaut-serde-jackson")

        implementation("ch.qos.logback:logback-classic")
        implementation("com.jongsoft.lang:language:$jongsoftLangVersion")

        testImplementation "org.junit.jupiter:junit-jupiter-api"
        testImplementation "io.micronaut.test:micronaut-test-junit5"
        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine"
        testImplementation("io.micronaut:micronaut-inject-java")
        testImplementation("org.assertj:assertj-core:$assertjVersion")
        testImplementation("org.mockito:mockito-core:$mockitoVersion")
    }
}
